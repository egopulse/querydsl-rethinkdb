plugins {
    id "java"
    id "idea"
    id "maven"
    id "com.jfrog.bintray" version "1.4"
}

group = 'com.egopulse'
version = '0.1.2'

description = """QueryDSL - RethinkDB support"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    provided
}

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}

sourceSets {
    test {
        java {
            srcDirs += "$buildDir/generated/test"
        }
    }
}

dependencies {
    compile group: 'com.querydsl', name: 'querydsl-core', version: '4.0.7'
    compile group: 'com.querydsl', name: 'querydsl-apt', version: '4.0.7'
    compile group: 'com.rethinkdb', name: 'rethinkdb-driver', version: '2.2-beta-1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

task generateQueryDSLQueryTypes(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.test.java
    classpath = configurations.compile + sourceSets.main.output + configurations.testCompile
    destinationDir = file("$buildDir/generated/test")
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.egopulse.querydsl.rethinkdb.RethinkDBAnnotationProcessor"
    ]
}

compileTestJava {
    dependsOn generateQueryDSLQueryTypes
}

test {
    classpath = project.sourceSets.test.runtimeClasspath + files("${projectDir}/build/generated/test")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name =  "${project.name}"
        userOrg = 'egopulse'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/egopulse/querydsl-rethinkdb.git'
    }
}

idea {
    module {
        excludeDirs -= file("$buildDir")
        sourceDirs += file("$buildDir/generated/test")
    }
}

